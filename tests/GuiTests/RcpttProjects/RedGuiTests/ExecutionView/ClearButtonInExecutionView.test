--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _1lEyQF06EeWeit3EM6sPPA,_d8aMACuvEeW667Afdo4DcA,_QflXwDDFEee-I-80nLsBOw
Element-Name: ClearButtonInExecutionView
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _2aQkYF4XEeWeit3EM6sPPA
Runtime-Version: 2.2.0.201706152316
Save-Time: 3/6/18 5:04 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

//run mixed tests
get-view "Project Explorer" | get-tree | select "ExecutionViewProject/normal_cases.robot" 
    | get-menu "Run As/1 Robot Test" | click
    
wait-for-console -times 5 -delay 1000   

//clear view 
with [get-view "Execution View"] {
    click
    get-button Clear | click
}

check-execution-view-labels -pass 0 -fail 0 -current_test 0 -all 0 -pb "0.0" -bar_color "no_color"
//check buttons
with [get-view "Execution View"] {
    get-button "Show Failures Only" | get-property enablement | equals true | verify-true
    get-button Clear | get-property enablement | equals true | verify-true
    get-button "Rerun Tests" | get-property enablement | equals false | verify-true
    get-button "Rerun Failed Tests" | get-property enablement | equals false | verify-true
}

//check text below tree
with [get-view "Execution View"] {
    get-tree | get-property itemCount | equals 0 | verify-true
    get-editbox -after [get-label "Failed: 0"] | get-property text | equals "" | verify-true
}

//press Show failures and Clear when view was cleared before - no effect
with [get-view "Execution View"] {
    get-button "Show Failures Only" | click
    get-button Clear | click
}

//run other test
get-view "Project Explorer" | get-tree | select "ExecutionViewProject/stopped_tests.robot" 
    | get-menu "Run As/1 Robot Test" | click

wait-for-console  
try -times 4 -delay 500 -command{
	get-view "Execution View" | get-label "Tests: 7/7" | get-property caption | equals "Tests: 7/7" | verify-true
}  

//click on test with error - text box will be filled    
with [get-view "Execution View"] {
    get-tree | select "ExecutionViewProject \(.*s\)/Stopped Tests \(.* s\)/here is fatal error \(.* s\)"
    get-button Clear | click
}

//after Clean text should be removed
with [get-view "Execution View"] {
    get-tree | get-property itemCount | equals 0 | verify-true
    get-editbox -after [get-label "Failed: 0"] | get-property text | equals "" | verify-true
}

check-execution-view-labels -pass 0 -fail 0  -current_test 0 -all 0 -pb "0.0" -bar_color "no_color"

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
