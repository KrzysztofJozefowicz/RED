--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: LibraryWithTheSameNameIn2Projects
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _vpSwwBuHEeezQPN39rc8sA
Runtime-Version: 2.1.0.201606221726
Save-Time: 4/7/17 2:12 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

//remove default project
get-view "Project Explorer" | get-tree | select RobotProject | get-menu Delete | click
with [get-window "Delete Resources"] {
    get-button "Delete project contents on disk (cannot be undone)" | check
    get-button OK | click
}

//add new project
get-view "Project Explorer" | get-tree | get-menu "New/Robot Project" | click
with [get-window "New Robot project"] {
    get-editbox -after [get-label "Project name:"] | set-text abc
    get-button Finish | click
}

//add new python file
get-view "Project Explorer" | get-tree | select abc | get-menu "New/Robot python file" | click
with [get-window "New Robot python file"] {
    get-editbox -after [get-label "File name:"] | set-text "my_lib"
    get-button Finish | click
}

//add new project
get-view "Project Explorer" | get-tree | select abc | get-menu "New/Robot Project" | click
with [get-window "New Robot project"] {
    get-editbox -after [get-label "Project name:"] | set-text def
    get-button Finish | click
}

//add pythob file with the same name as is was in first project
get-view "Project Explorer" | get-tree | select def | get-menu "New/Robot python file" | click
with [get-window "New Robot python file"] {
    get-editbox -after [get-label "File name:"] | set-text "my_lib.py"
    get-button Finish | click
}

//add content to first file
with [get-editor "my_lib.py" | get-text-viewer] {
    type-text "def fun():"
    key-type Enter
    key-type Tab
    type-text "return 9"
    key-type "M1+s"
}

//add content to second file
with [get-editor "my_lib.py" -index 1] {
    click
    with [get-text-viewer] {
        type-text "def from_second_project():"
        key-type Enter
        key-type Tab
        type-text "return 90"
        key-type "M1+s"
    }
}

//add file to red.xml
get-view "Project Explorer" | get-tree | select "abc/my_lib.py" | get-menu "Robot Framework/Add Library to red.xml" 
	| click
get-window "Select library class" | get-table | select "my_lib" | click -default
	
//add second file to second project	
get-view "Project Explorer" | get-tree | select "def/my_lib.py" | get-menu "Robot Framework/Add Library to red.xml" 
    | click
    
//check libsepcs    
with [get-view "Project Explorer" | get-tree] {
    get-item "abc/Robot Referenced libraries/my_lib abc \\(1\\)/Fun" | get-property index | equals 0 | verify-true
    get-item "abc/Robot Referenced libraries/my_lib abc \\(1\\)" | get-property childCount | equals 1 | verify-true
}
with [get-view "Project Explorer" | get-tree] {
    get-item "def/Robot Referenced libraries/my_lib def \\(1\\)/From Second Project" | get-property childCount 
        | equals 0 | verify-true
    get-item "def/Robot Referenced libraries/my_lib def \\(1\\)/From Second Project" | get-property index | equals 0 
        | verify-true
}

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
