--- RCPTT testcase ---
Format-Version: 1.0
Contexts: __If_UHjQEeWYifgNPLzx_Q
Element-Name: LinksToResourceWithAbsolutePath
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _rVkaEIb3EeWrUbOa1Nzguw
Runtime-Version: 2.0.2.201511100828
Save-Time: 3/22/16 3:06 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

//read path where this file is on disc
get-view "Project Explorer" | get-tree | select "RobotProject/resource_file.robot" 
    | select "RobotProject/resource_file.robot" | get-menu Properties | click

get-window "Properties for resource_file.robot" | get-editbox -after [get-label "Location:"] | get-property text -raw
   | let [val path_to_file -input]{
    
    get-window "Properties for resource_file.robot" | get-button Cancel | click
   
    //set absolute path
	get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | select "RobotProject/TestFile.robot" 
	    | double-click
	
	with [get-editor "TestFile.robot" | get-text-viewer] {
	    set-caret-pos 2 12
	    key-type "M2+END"
	    set-caret-pos 2 30
	    select-range 2 11 2 30
	    key-type BackSpace
	    type-text $path_to_file
		}

	//check that warning is in Problems
	get-tab-folder -index 4 | get-tab-item Problems | click
	wait 3000
	get-view Problems | get-tree | get-property itemCount | equals 1 | verify-true
	get-view Problems | get-tree | double-click
	with [get-view Problems | get-tree] {
	    get-item "Warnings \\(1 item\\)" | get-property "getItems().TreeItem[0].getText()" 
	        | equals [format "Path '%s' is absolute. It is preferred to use relative paths" $path_to_file]
	        | verify-true
	    get-item "Warnings \\(1 item\\)" | get-property childCount | equals 1 | verify-true
	}

}
//check if link to absolute file is working
with [get-editor "TestFile.robot" | get-text-viewer] {
    set-caret-pos 2 46
    open-declaration
}
with [get-tab-folder -index 1] {
    get-property "pages[0]" | equals "*TestFile.robot" | verify-true
    get-property "pages[1]" | equals "resource_file.robot" | verify-true
    get-property activePage | equals "resource_file.robot" | verify-true
    get-property tabCount | equals 2 | verify-true
}
get-editor "resource_file.robot" | get-text-viewer | get-property text | equals "*** Variables ***\n"
    + "${aga}  5\n"
    + "@{int_list}  1  2  3\n"
    + "${ALA}  text\n"
    + "#&{bands}  key=value" | verify-true
    
    
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
