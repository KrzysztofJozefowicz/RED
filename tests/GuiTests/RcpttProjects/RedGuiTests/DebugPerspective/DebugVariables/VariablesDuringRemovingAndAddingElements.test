--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _2Q6oEPOSEeaWpbE3jg39cA,_OCxtEBRGEeWWV9IuysVI_A,_jcbUQGu1Eeeps9cepQRw2Q
Element-Name: VariablesDuringRemovingAndAddingElements
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _oT2CUHwgEeeUiMBqkyjlBQ
Runtime-Version: 2.1.0.201606221726
Save-Time: 9/7/17 4:33 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "check-variables" [val dict_items_nr][val list_item_nr] [val dict_items] [val list_items]{

	get-view Variables | get-tree | get-item "&{dict}" | get-property childCount | equals $dict_items_nr | verify-true
	get-view Variables | get-tree | get-item "@{my_list}" | get-property childCount | equals $list_item_nr | verify-true
	
	$dict_items| each [val v] [val k]{	
		with [get-view Variables | get-tree] {
	    	get-item [concat "&{dict}/" $k] | get-property "values['Value']" | equals $v | verify-true
	    }
	}
	
	$list_items| each [val el] [val index]{	
		with [get-view Variables | get-tree] {
	    	get-item [format "@{my_list}/\\[%s\\]" $index ] | get-property "values['Value']" | equals $el | verify-true
	    }
	}
}	

get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | double-click

get-editor "TestFile.robot" | get-text-viewer | set-text  "*** Settings ***\n"
    + "Library    Collections    \n"
    + "*** Variables ***\n"
    + "@{my_list}    66  77  88\n"
    + "&{dict}    key=value    fruit= apple  wege=carrot  \n\n"
    + "*** Test Cases ***\n"
    + "ada\n"
    + "    Remove From Dictionary  ${dict}   fruit\n"
    + "    Remove From List    ${my_list}    2\n"
    + "    \n"
    + "    Append To List    ${my_list}   99  100\n"
    + "    Pop From Dictionary    ${dict}    wege\n"
    + "    Set To Dictionary   ${dict}         last  23\n"
    + "    Remove From List    ${my_list}    0\n"
    + "    log  8"
    
//save file and set BP
with [get-editor "TestFile.robot"] {
    with [get-text-viewer] {
        set-caret-pos 14 52
        key-type "M1+s"
    }
    with [get-left-ruler] {
        get-ruler-column AnnotationColumn | double-click-ruler 9
    }
}

exec-with-options -disableJobWaiting -command {
	get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | get-menu "Debug As/1 Robot Test" | click
	wait-for-specified_line-in-first-thread -line 9 -nr_of_frames 3
	
	wait-for-variables
	
	get-view Variables | get-tree | get-item "&{dict}" | expand
	get-view Variables | get-tree | get-item "@{my_list}" | expand
	
	check-variables -dict_items_nr 3 -list_item_nr 3 -dict_items [map [entry "fruit" " apple"] [entry "key" "value"] [entry wege carrot]] -list_items [list 66  77  88]
	
	get-button "Step Into (F5)" | click
	wait 2500
	check-variables -dict_items_nr 2 -list_item_nr 3 -dict_items [map  [entry "key" "value"] [entry wege carrot]] -list_items [list 66  77  88]
	
	get-button "Step Into (F5)" | click
	wait 2500
	check-variables -dict_items_nr 2 -list_item_nr 3 -dict_items [map  [entry "key" "value"] [entry wege carrot]] -list_items [list 66  77]
	
	get-button "Step Into (F5)" | click
	wait 2500
	check-variables -dict_items_nr 2 -list_item_nr 4 -dict_items [map  [entry "key" "value"] [entry wege carrot]] -list_items [list 66  77  99 100]
	
	get-button "Step Into (F5)" | click
	wait 2500
	check-variables -dict_items_nr 1 -list_item_nr 4 -dict_items [map  [entry "key" "value"]] -list_items [list 66  77  99 100]
	
	get-button "Step Into (F5)" | click
	wait 2500
	check-variables -dict_items_nr 2 -list_item_nr 4 -dict_items [map  [entry "key" "value"] [entry last  23]] -list_items [list 66  77  99 100]
	
	get-button "Step Into (F5)" | click
	wait 2500
	check-variables -dict_items_nr 2 -list_item_nr 4 -dict_items [map  [entry "key" "value"] [entry last  23]] -list_items [list  77  99 100]
}	   
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
