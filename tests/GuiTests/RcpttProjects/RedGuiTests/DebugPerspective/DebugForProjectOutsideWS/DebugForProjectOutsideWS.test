--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _oSud0JkDEeW1iIZN4tAN-A,_OCxtEBRGEeWWV9IuysVI_A
Element-Name: DebugForProjectOutsideWS
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _2yIsgJjpEeW1iIZN4tAN-A
Runtime-Version: 2.0.0.201506120617
Save-Time: 12/4/15 1:03 PM
Tags: Debug
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

get-java-property "user.home" |let [val user_home -input]{
	
	//import project outside of workspace
	get-menu "File/Import..." | click
	with [get-window Import | get-tree] {
	    select General | double-click
	    select "General/Existing Projects into Workspace" | double-click
	}
	//set path
	set-dialog-result Folder [concat $user_home "/Project1"]
	with [get-window Import] {
	    get-button "Browse..." | click
	    get-button Finish | click
	}
	
	//add BP
	get-view "Project Explorer" | get-tree | select "MediumRobotTest/Script.robot" | double-click
	with [get-editor "Script.robot" | get-left-ruler | get-ruler-column AnnotationColumn] {
	    double-click-ruler 19
	    double-click-ruler 21
	}
	exec-with-options -disableJobWaiting -command {
		get-view "Project Explorer" | get-tree | select "MediumRobotTest/Script.robot" | get-menu "Debug As/1 Robot Test" 
		    | click
		wait 5000
		try -times 3 -delay 1000 -command{
			get-view Debug | get-tree | get-item "Script.robot \\[Robot\\]/Robot Test at 127.0.0.1:.*/Thread \\[main\\]" 
	   			 | get-property childCount | equals 1 | verify-true
   		}
		check-debug-marker-on-ruler -line 19 -script_name  "Script.robot"
		get-button "Resume (F8)"| click
		wait 1000
		check-debug-marker-on-ruler -line 21 -script_name  "Script.robot"
		get-button "Resume (F8)"| click
		wait 2000
		get-view Variables | get-tree | get-property itemCount | equals 0 | verify-true
		get-view Debug | get-tree | get-item "<terminated>Script.robot \\[Robot\\]/<terminated, exit value: 0>.*" 
		    | get-property caption | equals "<terminated, exit value: 0>.*" |verify-true
		
	}
	//remove project
	uri-from-path [concat $user_home "/Project1"] | delete-file

}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
