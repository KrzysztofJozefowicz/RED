--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _d8aMACuvEeW667Afdo4DcA,_u48e4BYWEeeZSZgUeG7ZOA
Element-Name: PassingArgumentsToScriptCofig
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _FUFBcBYSEeeZSZgUeG7ZOA
Runtime-Version: 2.1.0.201606221726
Save-Time: 5/25/17 3:12 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "compare-console" [val new_args_list] [val inline_args] [val arg_file "Argument: --argumentfile\nArgument: .*\.arg\n"]{
	let [val new_args [concat-list-with-str $new_args_list -prefix "Argument: "  -sufix "\n"]]{
		try{
			get-view Console | get-text-viewer | get-property text 
			    | equals [format "Command: .*%s .*\n"
			    + "Suite Executor: Robot Framework .*\n" 
				+ "%s"
			    + "Argument: .*python.*\n"
			    + "Argument: -m\n"
			    + "Argument: robot.run\n"
			    + "Argument: --listener\n"
			    + "Argument: .*TestRunnerAgent.py:\d{1,6}\n"
			    + "%s"
			    + "Argument: .*ExecutorTab\n"
			    + "end" $inline_args $new_args $arg_file]| verify-true
		}-catch{
			get-view Console | get-text-viewer | get-property text 
			    | equals [format  "%s"
			    + "Argument: .*python.*\n"
			    + "Argument: -m\n"
			    + "Argument: robot.run\n"
			    + "Argument: --listener\n"
			    + "Argument: .*TestRunnerAgent.py:\d{1,6}\n"
			    + "%s"
			    + "Argument: .*ExecutorTab\n"
			    + "end\n" 
			    + "Command: .*%s .*\n"
			    + "Suite Executor: <unknown>"  $new_args $arg_file $inline_args]| verify-true
		
		}
	}
		     
}
//run test
get-view "Project Explorer" | get-tree | select "ExecutorTab/TestFile.robot" | get-menu "Run As/1 Robot Test" | click

//set path to script
get-button "Run.*" | click -arrow
get-menu "Run Configurations..." | click
with [get-window "Run Configurations" | get-tab-folder] {
    get-tab-item Executor | click
}
set-dialog-result File [format "platform:/resource/ExecutorTab/scripts/%s" [os-specific "script_win.bat" "script_lin.sh"]]

//add args and run test
with [get-window "Run Configurations"] {
    with [get-group "Executable file"] {
        get-button "Browse..." | click
        get-editbox -after [get-label "Additional executable file arguments:"] | set-text "arg1 arg2 \"arg 3\" --arg4 sum"
    }
    get-button Apply | click
    get-button Run | click
}

compare-console [list "arg1" "arg2" "arg 3" "--arg4" "sum"] -inline_args "arg1 arg2 \"arg 3\" --arg4 sum" 
 
//change prefernce to not use arg file
get-preferences-menu | click
with [get-window Preferences] {
    get-tree | select "Robot Framework/Launching"
    get-group Robot | get-button "Pass Robot arguments using arguments file" | uncheck
    get-button Apply | click
    get-button OK | click
}

//add tag
get-button "Run.*" | click -arrow
get-menu "Run Configurations..." | click
with [get-window "Run Configurations"] {
    with [get-group Tags] {
        get-button "Only run tests with these tags:" | check
        get-editbox | set-text tag1
        get-button "Add new tag" | click
    }
    get-button Apply | click
    get-button Run | click
}

//check console
compare-console [list "arg1" "arg2" "arg 3" "--arg4" "sum"] -inline_args "arg1 arg2 \"arg 3\" --arg4 sum" 
	-arg_file  [concat-list-with-str -list [list "-i" "tag1" "-s" "ExecutorTab.TestFile"] -prefix "Argument: " -sufix "\n"]

//set in prefernece to use single argument and run test	
get-preferences-menu | click
with [get-window Preferences] {
    get-group Executor | get-button "Use single argument to pass robot execution command line" | check
    get-button Apply | click
    get-button OK | click
}
get-button "Run.*" | click -arrow
get-menu "1 TestFile.robot" | click
try -command{
	get-view Console | get-text-viewer | get-property text 
	    | equals "Command: .*\n"
		    + "Suite Executor: Robot Framework .*\n"
		    + "Argument: arg1\n"
		    + "Argument: arg2\n"
		    + "Argument: arg 3\n"
		    + "Argument: --arg4\n"
		    + "Argument: sum\n"
		    + "Argument: .+python(\.exe) -m robot.run --listener .+TestRunnerAgent.py:\d{1,5} -i tag1 -s ExecutorTab.TestFile .+ExecutorTab\n"
		    + "end" | verify-true
}-catch{	
	get-view Console | get-text-viewer | get-property text 
    	| equals "Argument: arg1\n"
	    + "Argument: arg2\n"
	    + "Argument: arg 3\n"
	    + "Argument: --arg4\n"
	    + "Argument: sum\n"
	    + "Argument: .+python(\.exe)? -m robot.run --listener .+TestRunnerAgent.py:\d{1,5} -i tag1 -s ExecutorTab.TestFile .+ExecutorTab\n"
	    + "end\n" 
	    + "Command: .+\n"
	    + "Suite Executor: <unknown>"| verify-true
    
}	    		    
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
