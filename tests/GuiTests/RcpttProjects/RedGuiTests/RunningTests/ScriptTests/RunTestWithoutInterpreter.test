--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _d8aMACuvEeW667Afdo4DcA,_s-ctEA4_EeePcNCRX_abWA,_4UxogBh5Eee93KAG-tW51Q
Element-Name: RunTestWithoutInterpreter
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _E3JM0D-eEeejtMr_2XCc8w
Runtime-Version: 2.2.0.201706152316
Save-Time: 11/7/17 2:49 PM
Tags: Unstable
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

RED-750 - it will pass after correction termianting job
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

//remove all interpreters
get-preferences-menu | click
repeat  -times [get-window Preferences | get-table | get-property itemCount -raw] -command{
	with [get-window Preferences] {
	    get-table | get-item ".*" -index 0 | click
	    get-button Remove | click
	}
}
get-window Preferences | get-button $Apply_and_OK_in_preference | click
get-window Preferences | get-window "Rebuild required" | get-button OK | click
// change script content
//get-view "Project Explorer" | get-tree | select "ScriptProject/scripts/script_win.bat" 
//	    | get-menu "Open With/Text Editor" | click    
	    
if [get-by-os -linux "Linux" -win "Windows"| equals "Windows"]{
	get-view "Project Explorer" | get-tree | select "ScriptProject/scripts/script_win.bat" 
	    | get-menu "Open With/Text Editor" | click        
	get-editor "script_win.bat" | get-text-viewer | set-text "@echo off\n\n"
	    + ":loop1\n\n"
	    + "if \"%~1%\"==\"\" goto after_loop\n"
	    + "\techo Argument: %~1%\n"
	    + "\tshift\n"
	    + "\tgoto loop1\n\n"
	    + ":after_loop\n"
	    + "echo end" 
	get-editor script_win.bat | get-text-viewer  |key-type "M1+s"
}-else{
	get-view "Project Explorer" | get-tree | select "ScriptProject/scripts/script_lin.sh" 
	    | get-menu "Open With/Text Editor" | click    
	get-editor "script_lin.sh" | get-text-viewer | set-text "#!/bin/sh \n"
	    + "while [ \"$1\" != \"\" ]; do\n"
	    + "\techo Argument: $1\n"
	    + "    shift\n"
	    + "done\n\n"
	    + "echo end" 
	get-editor script_lin.sh | get-text-viewer  |key-type "M1+s"
}	    

// configure 'Run Configurations...' - set project name, add bat file
get-menu "Run/Run Configurations..." | click
with [get-window "Run Configurations"] {
    get-tree | select Robot | double-click
    get-group Project | get-button "Browse..." | click
    get-window "Select project" | get-tree | select ScriptProject | double-click
    get-tab-folder | get-tab-item Executor | click

	if [get-by-os -linux "Linux" -win "Windows"| equals "Windows"]{    
		set-dialog-result File "platform:/resource/ScriptProject/scripts/script_win.bat"
		with [get-window "Run Configurations"] {
		    get-group "Executable file" | get-button "Browse..." | click
		    get-button Apply | click
		    get-button Run | click
	}
	}-else{    
		set-dialog-result File "platform:/resource/ScriptProject/scripts/script_lin.sh"
		with [get-window "Run Configurations"] {
		    get-group "Executable file" | get-button "Browse..." | click
		    get-button Apply | click
		    get-button Run | click
		}
	}
}
// verify if there is no interpreter in Project Explorer and Console
get-view "Project Explorer" | get-tree | get-item "ScriptProject/Robot Standard libraries \\[???\\]" 
    | get-property caption | equals "Robot Standard libraries [???]" | verify-true
    
try -command{
	get-view Console | get-text-viewer | get-property text | equals "Argument: (python.exe|python)\n"
	    + "Argument: -m\n"
	    + "Argument: robot.run\n"
	    + "Argument: --listener\n"
	    + "Argument: .*\n"
	    + "Argument: --argumentfile\n"
	    + "Argument: .*\n"
	    + "Argument: .*\n"
	    + "end\n"
	    + "Command: .* (python.exe|python) -m robot.run --listener .* --argumentfile .* .*\n"
	    + "Suite Executor: <unknown>" | verify-true
}-catch{
	get-view Console | get-text-viewer | get-property text | equals "Command: .* (python.exe|python) -m robot.run --listener .* --argumentfile .* .*\n"
	    + "Suite Executor: <unknown>"
	    + "Argument: (python.exe|python)\n"
	    + "Argument: -m\n"
	    + "Argument: robot.run\n"
	    + "Argument: --listener\n"
	    + "Argument: .*\n"
	    + "Argument: --argumentfile\n"
	    + "Argument: .*\n"
	    + "Argument: .*\n"
	    + "end\n"  | verify-true
}
    	    
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
